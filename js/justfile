build: \
  (build-inner "changepoint") \
  (build-inner "clustering") \
  (build-inner "core") \
  (build-inner "dtw") \
  (build-inner "ets") \
  (build-inner "mstl") \
  (build-inner "outlier") \
  (build-inner "prophet") \
  (build-inner "seasons")
  #!/usr/bin/env bash
  set -euxo pipefail
  cp package.json.tmpl augurs/package.json
  VERSION=$(cargo metadata --format-version 1 | jq -r '.packages[] | select (.name == "augurs") | .version')
  jq < augurs/package.json ". | .version = \"$VERSION\"" > augurs/package.json.tmp
  mv augurs/package.json.tmp augurs/package.json

build-inner target args='':
  cd augurs-{{target}}-js && \
    rm -rf ./pkg && \
    wasm-pack build \
      --scope bsull \
      --out-name {{target}} \
      --release \
      --target web \
      --no-pack \
      --out-dir ../augurs \
      -- {{args}}

test:
  cd testpkg && \
    npm ci && \
    npm run typecheck && \
    npm run test:ci

publish:
  cd augurs && \
    node prepublish && \
    npm publish --access public

